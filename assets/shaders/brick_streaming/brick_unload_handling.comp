#version 450

#extension GL_EXT_debug_printf : disable
// debugPrintfEXT("hello world %f", 1.0);

#include "../raytracing/ray_commons.glsl"

layout(local_size_x_id = 0, local_size_y = 1, local_size_z = 1) in;

// Must be kept in sync with src/modules/voxel_rt/ray_pipeline_types.zig BrickGridState
layout (push_constant) readonly uniform BrickGridState {
    uint brick_count;
} brick_grid;

// TODO: dont care about brick set for this
layout (std430, set = 0, binding = 0) readonly buffer BrickSetBuffer {
    uint brick_set_bits[];
};
layout (std430, set = 0, binding = 1) buffer BrickIndex {
    uint brick_index_22b_req_count_8b_status_2b[];
};
layout (std430, set = 0, binding = 2) buffer BrickBuffer {
    Brick bricks[];
};

layout (std430, set = 1, binding = 0) buffer BrickRequestLimits {
    BrickLimits brick_limits;
};
layout (std430, set = 2, binding = 0) readonly buffer BrickUnloadRequestIndices {
    uint unload_request_brick_indices[];
};

void main() {
    const uint unload_request_index = gl_GlobalInvocationID.x;
    if (unload_request_index >= brick_limits.unload_request_count) {
        return;
    }

    uint remove_index = unload_request_brick_indices[unload_request_index];
    if (0 == brick_limits.active_bricks || remove_index >= brick_limits.active_bricks - 1) {
        return;
    }

    // We perform a swap remove to unload brick. 
    // This means we grab the last active brick and swap this the index requested to be removed
    const int swap_index = atomicAdd(brick_limits.active_bricks, -1) - 1;
    if (swap_index < 0) {
        // UB: we ignore RC and set the brick count to 0, assumption is that this will always lead to 0 in the case of empty 
        brick_limits.active_bricks = 0;
        return;
    }

    const uint remove_brick_meta_bits = brick_index_22b_req_count_8b_status_2b[remove_index];
    const uint removed_brick_index = bitfieldExtract(
        remove_brick_meta_bits,
        BRICK_INDEX_OFFSET,
        BRICK_INDEX_BITS
    );

    const uint swap_brick_meta_bits = brick_index_22b_req_count_8b_status_2b[swap_index];
    const uint swap_brick_index = bitfieldExtract(
        swap_brick_meta_bits,
        BRICK_INDEX_OFFSET,
        BRICK_INDEX_BITS
    );

    brick_index_22b_req_count_8b_status_2b[swap_index] = bitfieldInsert(
        swap_brick_meta_bits, 
        removed_brick_index, 
        BRICK_INDEX_OFFSET, 
        BRICK_INDEX_BITS
    );

    brick_index_22b_req_count_8b_status_2b[remove_index] = bitfieldInsert(
        remove_brick_meta_bits,
        BRICK_STATUS_UNLOADED,
        BRICK_STATUS_OFFSET,
        BRICK_STATUS_BITS
    );

    bricks[removed_brick_index] = bricks[swap_brick_index];
}

