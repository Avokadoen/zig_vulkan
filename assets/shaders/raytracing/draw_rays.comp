#version 450

#extension GL_EXT_debug_printf : disable
// debugPrintfEXT("hello world %f", 1.0);

// This extension is strictly not required for vulkan 1.2 target, but is used to silence validator...
#extension GL_GOOGLE_include_directive : enable
#include "ray_commons.glsl"

layout(local_size_x_id = 0, local_size_y_id = 1, local_size_z = 1) in;

layout(Rgba8, binding = 0) uniform writeonly image2D img_output;

layout (set = 0, binding = 1) buffer CursorBuffer
{
    RayBufferCursor cursor;
};
layout (set = 0, binding = 2) readonly buffer HitBuffer {
    HitRecord hit_records[];
};

// TOOD: https://bruop.github.io/exposure/
// source: https://www.shadertoy.com/view/WdjSW3
vec3 Tonemap_ACES(vec3 x) {
    // Narkowicz 2015, "ACES Filmic Tone Mapping Curve"
    const vec3 a = vec3(2.51);
    const vec3 b = vec3(0.03);
    const vec3 c = vec3(2.43);
    const vec3 d = vec3(0.59);
    const vec3 e = vec3(0.14);
    return (x * (a * x + b)) / (x * (c * x + d) + e);
}

shared uint warp_start_index;

void main() {
    if (gl_LocalInvocationIndex == 0) {
        const uint work_size = gl_WorkGroupSize.x * gl_WorkGroupSize.y * gl_WorkGroupSize.z;
        warp_start_index = work_size * atomicAdd(cursor.cursor, 1);
    }
    barrier();
    
    const uint hit_index = warp_start_index + gl_LocalInvocationIndex;
    if (hit_index >= cursor.max_index) {
        return;
    }

    const HitRecord hit_record = hit_records[hit_index];
    const ivec2 pixel_coord = ivec2(
        bitfieldExtract(hit_record.pixel_coord, 16, 16),
        bitfieldExtract(hit_record.pixel_coord, 0, 16)
    );
    imageStore(img_output, pixel_coord, vec4(hit_record.previous_color, 1.0));
}

